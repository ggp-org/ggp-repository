; Cit-Tac-Eot 
; by Lés Caméles Froliqué Ìlogicamént
; (Michael Abercrombie, Patrick Grafe, and Peter Pham)
(role xplayer)
(role oplayer)
(init (cell 1 1 b))
(init (cell 1 2 b))
(init (cell 1 3 b))
(init (cell 1 4 b))
(init (cell 1 5 b))
(init (cell 2 1 b))
(init (cell 2 2 b))
(init (cell 2 3 b))
(init (cell 2 4 b))
(init (cell 2 5 b))
(init (cell 3 1 b))
(init (cell 3 2 b))
(init (cell 3 3 b))
(init (cell 3 4 b))
(init (cell 3 5 b))
(init (cell 4 1 b))
(init (cell 4 2 b))
(init (cell 4 3 b))
(init (cell 4 4 b))
(init (cell 4 5 b))
(init (cell 5 1 b))
(init (cell 5 2 b))
(init (cell 5 3 b))
(init (cell 5 4 b))
(init (cell 5 5 b))
(init (control xplayer))
(<= (legal ?W (mark ?X ?Y)) (true (cell ?X ?Y b)) (true (control ?W)))
(<= (legal xplayer noop) (true (control oplayer)))
(<= (legal oplayer noop) (true (control xplayer)))
(<= (next (cell ?I ?J ?W)) futureline (true (cell ?I ?J ?W)) (distinct ?W b))
(<= (next (cell ?I ?J o)) futureline (true (cell ?I ?J b)) (does oplayer (mark ?I ?J)))
(<= (next (cell ?I ?J x)) futureline (true (cell ?I ?J b)) (does xplayer (mark ?I ?J)))
(<= (next (cell ?I ?J b)) futureline (true (cell ?I ?J b)) (does ?P (mark ?K ?L)) (distinct ?I ?K))
(<= (next (cell ?I ?J b)) futureline (true (cell ?I ?J b)) (does ?P (mark ?K ?L)) (distinct ?J ?L))
(<= (next (cell ?I ?J x)) (does xplayer (mark ?OldI ?OldJ)) (update ?OldI ?OldJ ?I ?J) (not futureline))
(<= (next (cell ?I ?J o)) (does oplayer (mark ?OldI ?OldJ)) (update ?OldI ?OldJ ?I ?J) (not futureline))
(<= (next (cell ?I ?J ?W)) (true (cell ?OldI ?OldJ ?W)) (update ?OldI ?OldJ ?I ?J) (distinct ?W b) (not futureline))
(<= (next (cell ?I ?J b)) (does ?W (mark ?K ?L)) (true (cell ?OldI ?OldJ b)) (update ?OldI ?OldJ ?I ?J) (distinct ?K ?OldI) (not futureline))
(<= (next (cell ?I ?J b)) (does ?W (mark ?K ?L)) (true (cell ?OldI ?OldJ b)) (update ?OldI ?OldJ ?I ?J) (distinct ?L ?OldJ) (not futureline))
(<= (sign o) (true (control oplayer)))
(<= (sign x) (true (control xplayer)))
(<= (futurerow ?M ?P ?W) (does ?P (mark ?M 1)) (sign ?W) (true (cell ?M 2 ?W)) (true (cell ?M 3 ?W)) (true (cell ?M 4 ?W)) (true (cell ?M 5 ?W)))
(<= (futurerow ?M ?P ?W) (true (cell ?M 1 ?W)) (does ?P (mark ?M 2)) (sign ?W) (true (cell ?M 3 ?W)) (true (cell ?M 4 ?W)) (true (cell ?M 5 ?W)))
(<= (futurerow ?M ?P ?W) (true (cell ?M 1 ?W)) (true (cell ?M 2 ?W)) (does ?P (mark ?M 3)) (sign ?W) (true (cell ?M 4 ?W)) (true (cell ?M 5 ?W)))
(<= (futurerow ?M ?P ?W) (true (cell ?M 1 ?W)) (true (cell ?M 2 ?W)) (true (cell ?M 3 ?W)) (does ?P (mark ?M 4)) (sign ?W) (true (cell ?M 5 ?W)))
(<= (futurerow ?M ?P ?W) (true (cell ?M 1 ?W)) (true (cell ?M 2 ?W)) (true (cell ?M 3 ?W)) (true (cell ?M 4 ?W)) (does ?P (mark ?M 5)) (sign ?W))
(<= (futurecolumn ?N ?P ?W) (does ?P (mark 1 ?N)) (sign ?W) (true (cell 2 ?N ?W)) (true (cell 3 ?N ?W)) (true (cell 4 ?N ?W)) (true (cell 5 ?N ?W)))
(<= (futurecolumn ?N ?P ?W) (true (cell 1 ?N ?W)) (does ?P (mark 2 ?N)) (sign ?W) (true (cell 3 ?N ?W)) (true (cell 4 ?N ?W)) (true (cell 5 ?N ?W)))
(<= (futurecolumn ?N ?P ?W) (true (cell 1 ?N ?W)) (true (cell 2 ?N ?W)) (does ?P (mark 3 ?N)) (sign ?W) (true (cell 4 ?N ?W)) (true (cell 5 ?N ?W)))
(<= (futurecolumn ?N ?P ?W) (true (cell 1 ?N ?W)) (true (cell 2 ?N ?W)) (true (cell 3 ?N ?W)) (does ?P (mark 4 ?N)) (sign ?W) (true (cell 5 ?N ?W)))
(<= (futurecolumn ?N ?P ?W) (true (cell 1 ?N ?W)) (true (cell 2 ?N ?W)) (true (cell 3 ?N ?W)) (true (cell 4 ?N ?W)) (does ?P (mark 5 ?N)) (sign ?W))
(<= (futurediagonal ?P ?W) (does ?P (mark 1 1)) (sign ?W) (true (cell 2 2 ?W)) (true (cell 3 3 ?W)) (true (cell 4 4 ?W)) (true (cell 5 5 ?W)))
(<= (futurediagonal ?P ?W) (true (cell 1 1 ?W)) (does ?P (mark 2 2)) (sign ?W) (true (cell 3 3 ?W)) (true (cell 4 4 ?W)) (true (cell 5 5 ?W)))
(<= (futurediagonal ?P ?W) (true (cell 1 1 ?W)) (true (cell 2 2 ?W)) (does ?P (mark 3 3)) (sign ?W) (true (cell 4 4 ?W)) (true (cell 5 5 ?W)))
(<= (futurediagonal ?P ?W) (true (cell 1 1 ?W)) (true (cell 2 2 ?W)) (true (cell 3 3 ?W)) (does ?P (mark 4 4)) (sign ?W) (true (cell 5 5 ?W)))
(<= (futurediagonal ?P ?W) (true (cell 1 1 ?W)) (true (cell 2 2 ?W)) (true (cell 3 3 ?W)) (true (cell 4 4 ?W)) (does ?P (mark 5 5)) (sign ?W))
(<= (futurediagonal ?P ?W) (does ?P (mark 1 5)) (sign ?W) (true (cell 2 4 ?W)) (true (cell 3 3 ?W)) (true (cell 4 2 ?W)) (true (cell 5 1 ?W)))
(<= (futurediagonal ?P ?W) (true (cell 1 5 ?W)) (does ?P (mark 2 4)) (sign ?W) (true (cell 3 3 ?W)) (true (cell 4 2 ?W)) (true (cell 5 1 ?W)))
(<= (futurediagonal ?P ?W) (true (cell 1 5 ?W)) (true (cell 2 4 ?W)) (does ?P (mark 3 3)) (sign ?W) (true (cell 4 2 ?W)) (true (cell 5 1 ?W)))
(<= (futurediagonal ?P ?W) (true (cell 1 5 ?W)) (true (cell 2 4 ?W)) (true (cell 3 3 ?W)) (does ?P (mark 4 2)) (sign ?W) (true (cell 5 1 ?W)))
(<= (futurediagonal ?P ?W) (true (cell 1 5 ?W)) (true (cell 2 4 ?W)) (true (cell 3 3 ?W)) (true (cell 4 2 ?W)) (does ?P (mark 5 1)) (sign ?W))
(<= futureline (futurerow ?M ?P ?W))
(<= futureline (futurecolumn ?M ?P ?W))
(<= futureline (futurediagonal ?P ?W))
(update 1 1 1 2)
(update 1 2 1 3)
(update 1 3 1 4)
(update 1 4 1 5)
(update 1 5 2 5)
(update 2 1 1 1)
(update 2 2 3 2)
(update 2 3 2 2)
(update 2 4 2 3)
(update 2 5 3 5)
(update 3 1 2 1)
(update 3 2 4 2)
(update 3 3 3 3)
(update 3 4 2 4)
(update 3 5 4 5)
(update 4 1 3 1)
(update 4 2 4 3)
(update 4 3 4 4)
(update 4 4 3 4)
(update 4 5 5 5)
(update 5 1 4 1)
(update 5 2 5 1)
(update 5 3 5 2)
(update 5 4 5 3)
(update 5 5 5 4)
(<= (next (control xplayer)) (true (control oplayer)))
(<= (next (control oplayer)) (true (control xplayer)))
(<= (row ?M ?W) (true (cell ?M 1 ?W)) (true (cell ?M 2 ?W)) (true (cell ?M 3 ?W)) (true (cell ?M 4 ?W)) (true (cell ?M 5 ?W)))
(<= (column ?N ?W) (true (cell 1 ?N ?W)) (true (cell 2 ?N ?W)) (true (cell 3 ?N ?W)) (true (cell 4 ?N ?W)) (true (cell 5 ?N ?W)))
(<= (diagonal ?W) (true (cell 1 1 ?W)) (true (cell 2 2 ?W)) (true (cell 3 3 ?W)) (true (cell 4 4 ?W)) (true (cell 5 5 ?W)))
(<= (diagonal ?W) (true (cell 1 5 ?W)) (true (cell 2 4 ?W)) (true (cell 3 3 ?W)) (true (cell 4 2 ?W)) (true (cell 5 1 ?W)))
(<= (line ?W) (row ?M ?W))
(<= (line ?W) (column ?M ?W))
(<= (line ?W) (diagonal ?W))
(<= open (true (cell ?M ?N b)))
(<= (goal xplayer 100) (line x) (not (line o)))
(<= (goal xplayer 50) (not (line x)) (not (line o)))
(<= (goal xplayer 50) (line x) (line o))
(<= (goal xplayer 0) (not (line x)) (line o))
(<= (goal oplayer 100) (not (line x)) (line o))
(<= (goal oplayer 50) (not (line x)) (not (line o)))
(<= (goal oplayer 50) (line x) (line o))
(<= (goal oplayer 0) (line x) (not (line o)))
(<= terminal (line x))
(<= terminal (line o))
(<= terminal (not open))