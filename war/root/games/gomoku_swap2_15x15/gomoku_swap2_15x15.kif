; Gomoku 15x15 with swap2
; GDL by Alex Landau

; This is an implementation of "standard" gomoku, in which rows of more than
; five pieces do not result in a win.

; This variant implements the "swap2" starting procedure, which is used
; in Gomoku tournaments. First, the first player places two black pieces and
; one white piece. Next, the second player may choose a color and proceed with
; play, or may choose to place two more pieces and give the choice of color
; back to the first player.

(role player1)
(role player2)

(color black)
(color white)

; Setup steps for swap2

(init (setupStep 1))
(<= (next (setupStep 2))
    (true (setupStep 1)))
(<= (next (setupStep 3))
    (true (setupStep 2)))
(<= (next (setupStep 4))
    (true (setupStep 3)))
(<= (next (setupStep 5))
    (true (setupStep 4))
    (does player2 (place ?x ?y)))
(<= (next (setupStep 6))
    (true (setupStep 5)))
(<= (next (control ?player))
    (does ?player (pick white)))
(<= (next (control ?opponent))
    (does ?player (pick black))
    (opponent ?player ?opponent))

(<= (next (isColor ?player ?color))
    (does ?player (pick ?color)))
(<= (next (isColor ?opponent ?otherColor))
    (does ?player (pick ?color))
    (opponent ?player ?opponent)
    (otherColor ?color ?otherColor))
(<= (next (isColor ?player ?color))
    (true (isColor ?player ?color)))

(<= (preplacementStep player1 black)
    (true (setupStep 1)))
(<= (preplacementStep player1 white)
    (true (setupStep 2)))
(<= (preplacementStep player1 black)
    (true (setupStep 3)))
(<= (preplacementStep player2 white)
    (true (setupStep 4)))
(<= (preplacementStep player2 black)
    (true (setupStep 5)))
(<= (preplacementStepForPlayer ?player)
    (preplacementStep ?player ?color))

(<= (legal ?player (place ?x ?y))
    (preplacementStep ?player ?color)
    (empty ?x ?y))

(<= (legal player2 (pick ?color))
    (color ?color)
    (true (setupStep 4)))
(<= (legal player1 (pick ?color))
    (color ?color)
    (true (setupStep 6)))

(<= (legal ?player noop)
    (role ?player)
    (not (true (control ?player)))
    (not (true (setupStep 6)))
    (not (preplacementStepForPlayer ?player)))
(<= (legal player2 noop)
    (true (setupStep 6)))
(<= (legal ?player (place ?x ?y))
    (true (control ?player))
    (empty ?x ?y))

(<= (empty ?x ?y)
    (index ?x)
    (index ?y)
    (not (occupied ?x ?y)))
(<= (occupied ?x ?y)
    (true (cell ?x ?y ?color)))
(<= anyEmptyCell
    (empty ?x ?y))

(<= (next (control player1))
    (true (control player2)))
(<= (next (control player2))
    (true (control player1)))
(<= (next (cell ?x ?y ?color))
    (true (cell ?x ?y ?color)))
(<= (next (cell ?x ?y ?color))
    (does ?player (place ?x ?y))
    (preplacementStep ?player ?color))
(<= (next (cell ?x ?y ?color))
    (does ?player (place ?x ?y))
    (true (isColor ?player ?color)))

(<= terminal
    (not anyEmptyCell))
(<= terminal
    (hasLine ?color))

(<= (goal ?player 100)
    (hasLine ?color)
    (true (isColor ?player ?color)))
(<= (goal ?player 0)
    (opponent ?player ?opponent)
    (true (isColor ?opponent ?theirColor))
    (hasLine ?theirColor))
(<= (goal ?player 50)
    (role ?player)
    (not (hasLine white))
    (not (hasLine black)))

; Horizontal line of exactly 5
(<= (hasLine ?color)
    (true (cell ?x1 ?y ?color))
    (succ ?x0 ?x1)
    (not (true (cell ?x0 ?y ?color)))
    (succ ?x1 ?x2)
    (true (cell ?x2 ?y ?color))
    (succ ?x2 ?x3)
    (true (cell ?x3 ?y ?color))
    (succ ?x3 ?x4)
    (true (cell ?x4 ?y ?color))
    (succ ?x4 ?x5)
    (true (cell ?x5 ?y ?color))
    (succ ?x5 ?x6)
    (not (true (cell ?x6 ?y ?color))))
; Vertical line of exactly five
(<= (hasLine ?color)
    (true (cell ?x ?y1 ?color))
    (succ ?y0 ?y1)
    (not (true (cell ?x ?y0 ?color)))
    (succ ?y1 ?y2)
    (true (cell ?x ?y2 ?color))
    (succ ?y2 ?y3)
    (true (cell ?x ?y3 ?color))
    (succ ?y3 ?y4)
    (true (cell ?x ?y4 ?color))
    (succ ?y4 ?y5)
    (true (cell ?x ?y5 ?color))
    (succ ?y5 ?y6)
    (not (true (cell ?x ?y6 ?color))))
; Diagonal line of exactly five
(<= (hasLine ?color)
    (true (cell ?x1 ?y1 ?color))
    (succ ?x0 ?x1)
    (succ ?y0 ?y1)
    (not (true (cell ?x0 ?y0 ?color)))
    (succ ?x1 ?x2)
    (succ ?y1 ?y2)
    (true (cell ?x2 ?y2 ?color))
    (succ ?x2 ?x3)
    (succ ?y2 ?y3)
    (true (cell ?x3 ?y3 ?color))
    (succ ?x3 ?x4)
    (succ ?y3 ?y4)
    (true (cell ?x4 ?y4 ?color))
    (succ ?x4 ?x5)
    (succ ?y4 ?y5)
    (true (cell ?x5 ?y5 ?color))
    (succ ?x5 ?x6)
    (succ ?y5 ?y6)
    (not (true (cell ?x6 ?y6 ?color))))
; Other diagonal line of exactly five
(<= (hasLine ?color)
    (true (cell ?x1 ?y1 ?color))
    (succ ?x0 ?x1)
    (succ ?y1 ?y0)
    (not (true (cell ?x0 ?y0 ?color)))
    (succ ?x1 ?x2)
    (succ ?y2 ?y1)
    (true (cell ?x2 ?y2 ?color))
    (succ ?x2 ?x3)
    (succ ?y3 ?y2)
    (true (cell ?x3 ?y3 ?color))
    (succ ?x3 ?x4)
    (succ ?y4 ?y3)
    (true (cell ?x4 ?y4 ?color))
    (succ ?x4 ?x5)
    (succ ?y5 ?y4)
    (true (cell ?x5 ?y5 ?color))
    (succ ?x5 ?x6)
    (succ ?y6 ?y5)
    (not (true (cell ?x6 ?y6 ?color))))

(opponent player1 player2)
(opponent player2 player1)
(otherColor white black)
(otherColor black white)
(index 1)
(index 2)
(index 3)
(index 4)
(index 5)
(index 6)
(index 7)
(index 8)
(index 9)
(index 10)
(index 11)
(index 12)
(index 13)
(index 14)
(index 15)

; Note: We extend the successor function past the indices to allow us to
; confirm that the space past the end of the line is not the player's color,
; in a general way.
(succ 0 1)
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)

(<= (base (control ?player))
    (role ?player))
(<= (base (cell ?x ?y ?color))
    (index ?x)
    (index ?y)
    (color ?color))
(base (setupStep 1))
(base (setupStep 2))
(base (setupStep 3))
(base (setupStep 4))
(base (setupStep 5))
(base (setupStep 6))
(<= (base (isColor ?player ?color))
    (role ?player)
    (color ?color))

(<= (input ?player noop)
    (role ?player))
(<= (input ?player (place ?x ?y))
    (role ?player)
    (index ?x)
    (index ?y))
(<= (input ?player (pick ?color))
    (role ?player)
    (color ?color))