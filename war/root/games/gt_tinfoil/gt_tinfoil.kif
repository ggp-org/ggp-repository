; "Tinfoil Hat" game
; by Alex Landau
;
; This is an iterated game-theoretical game. In each round, players perform
; one of three actions for points.
;
; Their first and second options are calling "heads" or "tails" for a simulated
; coin flip. (The caller wins if the choices are the same, the flipper if the choices
; are different.) The winner receives ten points, the loser none.
;
; The third option is to refuse to participate in the coin flip. This yields a consistent
; one point. The other player receives nine points, unless they also refuse to participate.
;
; The name of the game comes from the implications of choosing to not participate: this
; is only rational (risk-neutral) behavior if you think the opponent can read your mind!

(role flipper)
(role caller)

; Superset of actual possible scores
(<= (base (score ?player 0))
    (role ?player))
(<= (base (score ?player ?n))
    (role ?player)
    (succ ?m ?n))
(<= (base (step ?n))
    (sum ?n ?something 10))

(<= (input ?player heads)
    (role ?player))
(<= (input ?player tails)
    (role ?player))
(<= (input ?player neither)
    (role ?player))

(init (score flipper 0))
(init (score caller 0))
(init (step 0))

(<= (legal ?player heads)
    (role ?player))
(<= (legal ?player tails)
    (role ?player))
(<= (legal ?player neither)
    (role ?player))

(<= (wonFlip caller)
    (does flipper ?flip)
    (distinct ?flip neither)
    (does caller ?flip))

(<= (wonFlip flipper)
    (does flipper ?flip)
    (distinct ?flip neither)
    (does caller ?call)
    (distinct ?call neither)
    (distinct ?flip ?call))

(<= (roundScore ?player 10)
    (wonFlip ?player))
(<= (roundScore ?player 0)
    (wonFlip ?opponent)
    (opponent ?player ?opponent))

(<= (roundScore ?player 1)
    (does ?player neither))
(<= (roundScore ?player 9)
    (does ?opponent neither)
    (opponent ?player ?opponent)
    (does ?player ?something)
    (distinct ?something neither))

(<= (next (step ?np1))
    (true (step ?n))
    (succ ?n ?np1))
(<= (next (score ?player ?next))
    (true (score ?player ?cur))
    (roundScore ?player ?round)
    (sum ?cur ?round ?next))

(<= terminal
    (true (step 10)))
(<= (goal ?player ?totalScore)
    (true (score ?player ?totalScore)))

(opponent flipper caller)
(opponent caller flipper)

(<= (sum ?x ?y ?z)
    (succ ?ym1 ?y)
    (sum ?x ?ym1 ?zm1)
    (succ ?zm1 ?z))
(<= (sum ?x 0 ?x)
    (succ ?w ?x))
(sum 0 0 0)

(succ 0 1)
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)
(succ 16 17)
(succ 17 18)
(succ 18 19)
(succ 19 20)
(succ 20 21)
(succ 21 22)
(succ 22 23)
(succ 23 24)
(succ 24 25)
(succ 25 26)
(succ 26 27)
(succ 27 28)
(succ 28 29)
(succ 29 30)
(succ 30 31)
(succ 31 32)
(succ 32 33)
(succ 33 34)
(succ 34 35)
(succ 35 36)
(succ 36 37)
(succ 37 38)
(succ 38 39)
(succ 39 40)
(succ 40 41)
(succ 41 42)
(succ 42 43)
(succ 43 44)
(succ 44 45)
(succ 45 46)
(succ 46 47)
(succ 47 48)
(succ 48 49)
(succ 49 50)
(succ 50 51)
(succ 51 52)
(succ 52 53)
(succ 53 54)
(succ 54 55)
(succ 55 56)
(succ 56 57)
(succ 57 58)
(succ 58 59)
(succ 59 60)
(succ 60 61)
(succ 61 62)
(succ 62 63)
(succ 63 64)
(succ 64 65)
(succ 65 66)
(succ 66 67)
(succ 67 68)
(succ 68 69)
(succ 69 70)
(succ 70 71)
(succ 71 72)
(succ 72 73)
(succ 73 74)
(succ 74 75)
(succ 75 76)
(succ 76 77)
(succ 77 78)
(succ 78 79)
(succ 79 80)
(succ 80 81)
(succ 81 82)
(succ 82 83)
(succ 83 84)
(succ 84 85)
(succ 85 86)
(succ 86 87)
(succ 87 88)
(succ 88 89)
(succ 89 90)
(succ 90 91)
(succ 91 92)
(succ 92 93)
(succ 93 94)
(succ 94 95)
(succ 95 96)
(succ 96 97)
(succ 97 98)
(succ 98 99)
(succ 99 100)