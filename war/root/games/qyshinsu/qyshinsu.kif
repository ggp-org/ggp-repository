;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; Qyshinsu
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; ROLE Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role black)
(role red)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; INIT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(init (position 1 empty))
(init (position 2 empty))
(init (position 3 empty))
(init (position 4 empty))
(init (position 5 empty))
(init (position 6 empty))
(init (position 7 empty))
(init (position 8 empty))
(init (position 9 empty))
(init (position 10 empty))
(init (position 11 empty))
(init (position 12 empty))

(init (control black))
(init (step 1))

(init (legalPlayLoc 1))
(init (legalPlayLoc 2))
(init (legalPlayLoc 3))
(init (legalPlayLoc 4))
(init (legalPlayLoc 5))
(init (legalPlayLoc 6))
(init (legalPlayLoc 7))
(init (legalPlayLoc 8))
(init (legalPlayLoc 9))
(init (legalPlayLoc 10))
(init (legalPlayLoc 11))
(init (legalPlayLoc 12))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; LEGAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal ?player (add ?pos ?stone))
    (true (control ?player))
    (true (position ?pos empty))
    (true (legalPlayLoc ?pos))
	(isStone ?stone)
    (not (alreadyTwoOf ?stone))
    (not (currentPlayerRemovedLastTurn ?stone ?pos)))
    
(<= (legal ?player (remove ?pos ?stone))
    (true (control ?player))
    (true (owner ?pos ?player))
    (true (legalPlayLoc ?pos))
	(true (position ?pos ?stone)))
    
(<= (legal red noop)
    (true (control black)))
(<= (legal black noop)
    (true (control red)))
    
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; NEXT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (next (control red))
	(true (control black)))
	
(<= (next (control black))
	(true (control red)))
	
(<= (next (step ?stepNum))
	(true (step ?oldStep))
	(succ ?oldStep ?stepNum))

(<= (next (owner ?pos ?player))
	(true (owner ?pos ?player))
	(true (position ?pos ?stone))
	(not (does ?player (remove ?pos ?stone))))

(<= (next (owner ?pos ?player))
	(does ?player (add ?pos ?stone)))

(<= (next (position ?pos empty))
	(true (position ?pos empty))	
	(not (stoneAddedToPos ?pos)))

(<= (stoneAddedToPos ?pos)
	(does ?player (add ?pos ?stone)))
	
(<= (next (position ?pos ?stone))
	(isStone ?stone)
	(true (position ?pos ?stone))
	(not (stoneRemovedFromPos ?pos)))
	
(<= (stoneRemovedFromPos ?pos)
	(does ?player (remove ?pos ?stone)))
    
(<= (next (position ?pos empty))
    (does ?player (remove ?pos ?stone)))
    
(<= (next (position ?pos ?stone))
    (does ?player (add ?pos ?stone)))
	
;;Add or remove normal stone
;;(Note that because diff is undefined for (diff ?x ?x), ?stone cannot be the old stone
(<= (next (legalPlayLoc ?pos))
	(isNumber ?pos)
	(does ?player (add ?pos2 ?stone))
	(diff ?pos ?pos2 ?stone))
	
(<= (next (legalPlayLoc ?pos))
	(isNumber ?pos)
	(does ?player (remove ?pos2 ?stone))
	(diff ?pos ?pos2 ?stone))
	
;;OldStoneAdded Condition
(<= (next (legalPlayLoc ?pos))
	(isNumber ?pos)
	(isOldStone ?stone)
	(does ?player (add ?pos2 ?stone))
	(closestEmptyPosition ?pos2 ?pos))

;;OldStoneRemoved Condition	
(<= (next (legalPlayLoc ?pos))
	(isNumber ?pos)
	(isOldStone ?stone)
	(does ?player (remove ?pos2 ?stone))
	(role ?player2)
	(distinct ?player ?player2)
	(closestPlayerOccupiedPosition ?player2 ?pos2 ?pos))
	
(<= (next oldStoneAdded)
	(does ?player (add ?pos ?stone))
	(isOldStone ?stone)
	(isNumber ?pos)
	(role ?player))

(<= (next oldStoneRemoved)
	(does ?player (remove ?pos ?stone))
	(isOldStone ?stone)
	(isNumber ?pos)
	(role ?player))
	
(<= (next (redPlayerRemovedLastTurn ?stone ?pos))
	(does red (remove ?pos ?stone)))
	
(<= (next (redPlayerRemovedLastTurn ?stone ?pos))
	(true (redPlayerRemovedLastTurn ?stone ?pos))
	(true (control black)))
	
(<= (next (blackPlayerRemovedLastTurn ?stone ?pos))
	(does black (remove ?pos ?stone)))
	
;;If it's red turn propogate the black player's last move
(<= (next (blackPlayerRemovedLastTurn ?stone ?pos))
	(true (blackPlayerRemovedLastTurn ?stone ?pos))
	(true (control red)))
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; GOAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal red 100)
    redWin)
(<= (goal red 0)
    (not redWin))
    
(<= (goal black 100)
    blackWin)
(<= (goal black 0)
	(not blackWin))
		
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; TERMINAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= terminal 
    redWin)
(<= terminal
    blackWin)
(<= terminal
	(true (step 50)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; View Definitions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (currentPlayerRemovedLastTurn ?stone ?pos)
	(true (redPlayerRemovedLastTurn ?stone ?pos))
	(true (control red)))
	
(<= (currentPlayerRemovedLastTurn ?stone ?pos)
	(true (blackPlayerRemovedLastTurn ?stone ?pos))
	(true (control black)))

(<= (closestEmptyPosition ?pos1 ?pos2)
	(true (position ?pos2 empty))
	(isNumber ?pos1)
	(isNumber ?pos2)	
	(distinct ?pos1 ?pos2)
	(not (closerEmptyPosition ?pos1 ?pos2)))
		 
(<= (closerEmptyPosition ?pos1 ?pos2)
	(true (position ?pos3 empty))
	(isNumber ?pos1)
	(isNumber ?pos2)
	(isNumber ?pos3)
	(distinct ?pos3 ?pos1)
	(distinct ?pos3 ?pos2)	
	(diff ?pos1 ?pos2 ?a)
	(diff ?pos1 ?pos3 ?b)
	(less ?b ?a))
	
(<= (closestPlayerOccupiedPosition ?player ?pos1 ?pos2)
	(true (owner ?pos2 ?player))
	(isNumber ?pos1)
	(isNumber ?pos2)
	(role ?player)
	(distinct ?pos1 ?pos2)
	(not (closerPlayerOccupiedPosition ?player ?pos1 ?pos2)))
	
(<= (closerPlayerOccupiedPosition ?player ?pos1 ?pos2)
	(true (owner ?pos3 ?player))
	(isNumber ?pos1)
	(isNumber ?pos2)
	(isNumber ?pos3)
	(role ?player)
	(distinct ?pos3 ?pos2)
	(distinct ?pos3 ?pos1)
	(diff ?pos1 ?pos2 ?a)
	(diff ?pos1 ?pos3 ?b)
	(less ?b ?a))
	
(<= (alreadyTwoOf ?stone)
	(true (position ?pos1 ?stone))
	(true (position ?pos2 ?stone))
	(distinct ?pos1 ?pos2))

(<= redWin
	(not (hasLegalMove black))
	(true (control black)))
	
(<= blackWin
	(not (hasLegalMove red))
	(true (control red)))
	
(<= (hasLegalMove ?player)
	(role ?player)
	(isNumber ?pos)
	(isStone ?stone)
	(legal ?player (add ?pos ?stone)))
	
(<= (hasLegalMove ?player)
	(role ?player)
	(isNumber ?pos)
	(isStone ?stone)
	(legal ?player (remove ?pos ?stone)))
	
(<= (next (hadLegalMove ?player))
	(hasLegalMove ?player))
	
;;;;;;;;;;;;;;;;;
;;;;;;;MATH;;;;;;
;;;;;;;;;;;;;;;;;

(<= (less ?x ?y)
	(Nplusplus ?x ?a)
	(less ?a ?y))
	
(<= (less ?x ?y)
	(Nplusplus ?x ?y))
	
;;Normal plusplus, not modular
(Nplusplus 1 2) (Nplusplus 2 3) (Nplusplus 3 4) (Nplusplus 4 5) (Nplusplus 5 6) 

(<= (diff ?x ?y 1)
	(plusplus ?x ?y))
	
(<= (diff ?x ?y 1)
	(plusplus ?y ?x))
	
(<= (diff ?x ?y 2)
	(plus2 ?x ?y))
	
(<= (diff ?x ?y 2)
	(plus2 ?y ?x))

(<= (diff ?x ?y 3)
	(plus3 ?x ?y))
	
(<= (diff ?x ?y 3)
	(plus3 ?y ?x))
	
(<= (diff ?x ?y 4)
	(plus4 ?x ?y))
	
(<= (diff ?x ?y 4)
	(plus4 ?y ?x))
	
(<= (diff ?x ?y 5)
	(plus5 ?x ?y))
	
(<= (diff ?x ?y 5)
	(plus5 ?y ?x))
	
(<= (diff ?x ?y 6)
	(plus6 ?x ?y))
	
(<= (diff ?x ?y 6)
	(plus6 ?y ?x))

(<= (plus6 ?x ?y)
	(plus5 ?x ?c)
	(plusplus ?c ?y))

(<= (plus5 ?x ?y)
	(plus4 ?x ?c)
	(plusplus ?c ?y))

(<= (plus4 ?x ?y)
	(plus3 ?x ?c)
	(plusplus ?c ?y))

(<= (plus3 ?x ?y)
	(plus2 ?x ?c)
	(plusplus ?c ?y))

(<= (plus2 ?x ?y)
	(plusplus ?x ?c)
	(plusplus ?c ?y))
    
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Static Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(isStone 0) (isOldStone 0)
(isStone 1) (isNotOldStone 1)
(isStone 2) (isNotOldStone 2)
(isStone 3) (isNotOldStone 3)
(isStone 4) (isNotOldStone 4)
(isStone 5) (isNotOldStone 5)

(isNumber 1) (isNumber 2) (isNumber 3) (isNumber 4) (isNumber 5) (isNumber 6) (isNumber 7) (isNumber 8) (isNumber 9) (isNumber 10) (isNumber 11) (isNumber 12)

(plusplus 1 2)  (plusplus 2 3)  (plusplus 3 4)  (plusplus 4 5)  (plusplus 5 6)  (plusplus 6 7)  (plusplus 7 8)  (plusplus 8 9)  (plusplus 9 10)  (plusplus 10 11)  (plusplus 11 12)  (plusplus 12 1)

(succ  1  2) (succ  2  3) (succ  3  4) (succ  4  5) (succ  5  6) 
(succ  6  7) (succ  7  8) (succ  8  9) (succ  9 10) (succ 10 11)
(succ 11 12) (succ 12 13) (succ 13 14) (succ 14 15) (succ 15 16)
(succ 16 17) (succ 17 18) (succ 18 19) (succ 19 20) (succ 20 21)
(succ 21 22) (succ 22 23) (succ 23 24) (succ 24 25) (succ 25 26)
(succ 26 27) (succ 27 28) (succ 28 29) (succ 29 30) (succ 30 31)
(succ 31 32) (succ 32 33) (succ 33 34) (succ 34 35) (succ 35 36)
(succ 36 37) (succ 37 38) (succ 38 39) (succ 39 40) (succ 40 41)
(succ 41 42) (succ 42 43) (succ 43 44) (succ 44 45) (succ 45 46)
(succ 46 47) (succ 47 48) (succ 48 49) (succ 49 50) (succ 50 51)
(succ 51 52) (succ 52 53) (succ 53 54) (succ 54 55) (succ 55 56)
(succ 56 57) (succ 57 58) (succ 58 59) (succ 59 60) (succ 60 61)
(succ 61 62) (succ 62 63) (succ 63 64) (succ 64 65) (succ 65 66)
(succ 66 67) (succ 67 68) (succ 68 69) (succ 69 70) (succ 70 71)
(succ 71 72) (succ 72 73) (succ 73 74) (succ 74 75) (succ 75 76)
(succ 76 77) (succ 77 78) (succ 78 79) (succ 79 80) (succ 80 81)
(succ 81 82) (succ 82 83) (succ 83 84) (succ 84 85) (succ 85 86)
(succ 86 87) (succ 87 88) (succ 88 89) (succ 89 90) (succ 90 91)
(succ 91 92) (succ 92 93) (succ 93 94) (succ 94 95) (succ 95 96)
(succ 96 97) (succ 97 98) (succ 98 99) (succ 99 100)