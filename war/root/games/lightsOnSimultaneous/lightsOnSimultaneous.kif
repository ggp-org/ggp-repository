;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; LightsOn (Four games played simultaneously)
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; ROLEs
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role robot)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; INITs
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(init (cell 1 1 0))
(init (cell 1 2 0))
(init (cell 2 1 0))
(init (cell 2 2 0))

(init (cell 3 1 0))
(init (cell 3 2 0))
(init (cell 4 1 0))
(init (cell 4 2 0))

(init (cell 1 3 0))
(init (cell 1 4 0))
(init (cell 2 3 0))
(init (cell 2 4 0))

(init (cell 3 3 0))
(init (cell 3 4 0))
(init (cell 4 3 0))
(init (cell 4 4 0))

(init (step 0))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; LEGALs
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal robot (press ?x1 ?y1 ?x2 ?y2))
	(index ?x1)
	(index ?y1)
	(index ?x2)
	(index ?y2))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; NEXTs
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (next (cell ?x ?y 4))
	(does robot (press ?x ?y ?xIgnore ?yIgnore)))
(<= (next (cell ?x ?y 4))
	(does robot (press ?xIgnore ?yIgnore ?x ?y)))

(<= (next (cell ?x ?y 3))
	(true (cell ?x ?y 4)))

(<= (next (cell ?x ?y 2))
	(true (cell ?x ?y 3)))

(<= (next (cell ?x ?y 1))
	(true (cell ?x ?y 2)))

(<= (next (cell ?x ?y 0))
	(true (cell ?x ?y 1)))

(<= (next (cell ?x ?y 0))
	(true (cell ?x ?y 0)))
	
(<= (next (step ?b))
    (true (step ?a))
    (succ ?a ?b))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; TERMINALs
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= terminal
    lightsOn1)
(<= terminal
    lightsOn2)
(<= terminal
    lightsOn3)
(<= terminal
    lightsOn4)
    
(<= terminal
    (true (step 20)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; GOALs
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal robot 100)
	lightsOn1)
(<= (goal robot 100)
	lightsOn2)
(<= (goal robot 100)
	lightsOn3)
(<= (goal robot 100)
	lightsOn4)

(<= (goal robot 0)
    (not lightsOn1)
    (not lightsOn2)
    (not lightsOn3)
    (not lightsOn4))
    
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; View Rules
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (on ?x ?y)
	(true (cell ?x ?y 1)))
(<= (on ?x ?y)
	(true (cell ?x ?y 2)))
(<= (on ?x ?y)
	(true (cell ?x ?y 3)))
(<= (on ?x ?y)
	(true (cell ?x ?y 4)))

(<= lightsOn1
	(on 1 1)
	(on 1 2)
	(on 2 1)
	(on 2 2))

(<= lightsOn2
	(on 3 1)
	(on 3 2)
	(on 4 1)
	(on 4 2))

(<= lightsOn3
	(on 1 3)
	(on 1 4)
	(on 2 3)
	(on 2 4))

(<= lightsOn4
	(on 3 3)
	(on 3 4)
	(on 4 3)
	(on 4 4))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Static Sentences
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(index 1) (index 2) (index 3) (index 4)

(succ 0 1)
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)
(succ 16 17)
(succ 17 18)
(succ 18 19)
(succ 19 20)