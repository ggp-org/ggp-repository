; Guess Two-Thirds of the Average (two players)
; A one-shot game theoretical game.
; The goal is to guess (closer than any other player) two-thirds of the average
; of all guesses.

(role player1)
(role player2)

(<= (base (guessed ?player ?number))
    (role ?player)
    (guessableNumber ?number))

(<= (input ?player (guess ?number))
    (role ?player)
    (guessableNumber ?number))

(<= (legal ?player (guess ?number))
    (role ?player)
    (guessableNumber ?number))

(<= (next (guessed ?player ?number))
    (does ?player (guess ?number)))

; Find the total guessed
(<= (total ?number)
    (true (guessed player1 ?n1))
    (true (guessed player2 ?n2))
    (sum ?n1 ?n2 ?number))

; Find two-thirds of the average, rounded to nearest integer
; Divide total by 2, multiply by 2/3; or just divide by 3
; One of these three will apply, based on the total modulo 3
(<= (twoThirdsAverage ?out)
    (total ?total)
    (times3 ?out ?total))
(<= (twoThirdsAverage ?out)
    (total ?total)
    (succ ?total ?tp1)
    (times3 ?out ?tp1))
(<= (twoThirdsAverage ?out)
    (total ?total)
    (succ ?tm1 ?total)
    (times3 ?out ?tm1))

(<= (closeness ?player ?score)
    (true (guessed ?player ?guess))
    (twoThirdsAverage ?result)
    (absDiff ?result ?guess ?score))

(<= (notClosest ?player)
    (closeness ?player ?score)
    (closeness ?otherPlayer ?lowerScore)
    (distinct ?player ?otherPlayer)
    (lt ?lowerScore ?score))

(<= (closest ?player)
    (role ?player)
    (not (notClosest ?player)))

(<= (goal player1 100)
    (closest player1)
    (notClosest player2))
(<= (goal player2 100)
    (notClosest player1)
    (closest player2))

(<= (goal ?player 50)
    (role ?player)
    (closest player1)
    (closest player2))

(<= (goal ?player 0)
    (notClosest ?player))

; Single step
(<= terminal
    (true (guessed ?player ?number)))

; Mathematical basics

; Absolute value of difference: |x - y| = z
; x and y are defined, looking for z
(<= (absDiff ?x ?x 0)
    (anyNumber ?x))
(<= (absDiff ?x ?y ?z)
    (lt ?y ?x)
    (succ ?xm1 ?x)
    (absDiff ?xm1 ?y ?zm1)
    (succ ?zm1 ?z))
(<= (absDiff ?x ?y ?z)
    (lt ?x ?y)
    (succ ?ym1 ?y)
    (absDiff ?x ?ym1 ?zm1)
    (succ ?zm1 ?z))

; x + y = z
; x and y are defined, looking for z
(<= (sum ?x 0 ?x)
    (anyNumber ?x))
(<= (sum ?x ?y ?z)
    (succ ?ym1 ?y)
    (sum ?x ?ym1 ?zm1)
    (succ ?zm1 ?z))
    
; 3x = y
; y is defined, looking for x
(times3 0 0)
(<= (times3 ?x ?y)
    (succ ?ym1 ?y)
    (succ ?ym2 ?ym1)
    (succ ?ym3 ?ym2)
    (times3 ?xm1 ?ym3)
    (succ ?xm1 ?x))

; x < y
(<= (lt ?x ?y)
    (lte ?x ?y)
    (distinct ?x ?y))

; x <= y
(<= (lte ?x ?x)
    (anyNumber ?x))
(<= (lte ?x ?y)
    (succ ?ym1 ?y)
    (lte ?x ?ym1))

(anyNumber 0)
(<= (anyNumber ?n)
    (succ ?m ?n))

(<= (guessableNumber ?number)
    (lte ?number 100))

; Successor goes to 201 so twoThirdsAverage will work right with (times3 67 201) if total is 200
(succ 0 1)
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)
(succ 16 17)
(succ 17 18)
(succ 18 19)
(succ 19 20)
(succ 20 21)
(succ 21 22)
(succ 22 23)
(succ 23 24)
(succ 24 25)
(succ 25 26)
(succ 26 27)
(succ 27 28)
(succ 28 29)
(succ 29 30)
(succ 30 31)
(succ 31 32)
(succ 32 33)
(succ 33 34)
(succ 34 35)
(succ 35 36)
(succ 36 37)
(succ 37 38)
(succ 38 39)
(succ 39 40)
(succ 40 41)
(succ 41 42)
(succ 42 43)
(succ 43 44)
(succ 44 45)
(succ 45 46)
(succ 46 47)
(succ 47 48)
(succ 48 49)
(succ 49 50)
(succ 50 51)
(succ 51 52)
(succ 52 53)
(succ 53 54)
(succ 54 55)
(succ 55 56)
(succ 56 57)
(succ 57 58)
(succ 58 59)
(succ 59 60)
(succ 60 61)
(succ 61 62)
(succ 62 63)
(succ 63 64)
(succ 64 65)
(succ 65 66)
(succ 66 67)
(succ 67 68)
(succ 68 69)
(succ 69 70)
(succ 70 71)
(succ 71 72)
(succ 72 73)
(succ 73 74)
(succ 74 75)
(succ 75 76)
(succ 76 77)
(succ 77 78)
(succ 78 79)
(succ 79 80)
(succ 80 81)
(succ 81 82)
(succ 82 83)
(succ 83 84)
(succ 84 85)
(succ 85 86)
(succ 86 87)
(succ 87 88)
(succ 88 89)
(succ 89 90)
(succ 90 91)
(succ 91 92)
(succ 92 93)
(succ 93 94)
(succ 94 95)
(succ 95 96)
(succ 96 97)
(succ 97 98)
(succ 98 99)
(succ 99 100)
(succ 100 101)
(succ 101 102)
(succ 102 103)
(succ 103 104)
(succ 104 105)
(succ 105 106)
(succ 106 107)
(succ 107 108)
(succ 108 109)
(succ 109 110)
(succ 110 111)
(succ 111 112)
(succ 112 113)
(succ 113 114)
(succ 114 115)
(succ 115 116)
(succ 116 117)
(succ 117 118)
(succ 118 119)
(succ 119 120)
(succ 120 121)
(succ 121 122)
(succ 122 123)
(succ 123 124)
(succ 124 125)
(succ 125 126)
(succ 126 127)
(succ 127 128)
(succ 128 129)
(succ 129 130)
(succ 130 131)
(succ 131 132)
(succ 132 133)
(succ 133 134)
(succ 134 135)
(succ 135 136)
(succ 136 137)
(succ 137 138)
(succ 138 139)
(succ 139 140)
(succ 140 141)
(succ 141 142)
(succ 142 143)
(succ 143 144)
(succ 144 145)
(succ 145 146)
(succ 146 147)
(succ 147 148)
(succ 148 149)
(succ 149 150)
(succ 150 151)
(succ 151 152)
(succ 152 153)
(succ 153 154)
(succ 154 155)
(succ 155 156)
(succ 156 157)
(succ 157 158)
(succ 158 159)
(succ 159 160)
(succ 160 161)
(succ 161 162)
(succ 162 163)
(succ 163 164)
(succ 164 165)
(succ 165 166)
(succ 166 167)
(succ 167 168)
(succ 168 169)
(succ 169 170)
(succ 170 171)
(succ 171 172)
(succ 172 173)
(succ 173 174)
(succ 174 175)
(succ 175 176)
(succ 176 177)
(succ 177 178)
(succ 178 179)
(succ 179 180)
(succ 180 181)
(succ 181 182)
(succ 182 183)
(succ 183 184)
(succ 184 185)
(succ 185 186)
(succ 186 187)
(succ 187 188)
(succ 188 189)
(succ 189 190)
(succ 190 191)
(succ 191 192)
(succ 192 193)
(succ 193 194)
(succ 194 195)
(succ 195 196)
(succ 196 197)
(succ 197 198)
(succ 198 199)
(succ 199 200)
(succ 200 201)