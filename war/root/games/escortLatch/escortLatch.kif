; Escort breakthrough, with an explicit latch
; by Alex Landau, 5/28/11
; 
; This is a breakthrough variant in which each side has a king as well
; as a phalanx of pawns. Players only win if their king makes it to the
; other side; pawns don't count.
;
; The game description is slightly contrived in order to make the presence
; of a latch easy to detect. It would be interesting to see which latch
; detection techniques could work on a more general version of the game.
;
; Will need a step counter, unlike most versions of breakthrough.
;
; Kings are constrained so they can't move backwards, mostly to help
; guide depth charges. (They can still move sideways.)

(role white)
(role black)

(init (cell a 2 wp))
(init (cell b 2 wp))
(init (cell c 2 wp))
(init (cell d 2 wp))
(init (cell e 2 wp))
(init (cell f 2 wp))
(init (cell g 2 wp))
(init (cell h 2 wp))
(init (cell d 1 wk))

(init (cell a 7 bp))
(init (cell b 7 bp))
(init (cell c 7 bp))
(init (cell d 7 bp))
(init (cell e 7 bp))
(init (cell f 7 bp))
(init (cell g 7 bp))
(init (cell h 7 bp))
(init (cell d 8 bk))

(init (control white))
(init (step 1))

(<= (next (control white))
	(true (control black)))
(<= (next (control black))
	(true (control white)))
	
(<= (legal white noop)
	(true (control black)))
(<= (legal black noop)
	(true (control white)))
(<= (legal white noop)
	(not (hasAnyPiece white)))
(<= (legal black noop)
	(not (hasAnyPiece black)))
(<= (legal ?player noop)
    (true (control ?player))
    (not (hasAnyLegalMove ?player)))

(<= (hasAnyLegalMove ?player)
    (legal2 ?player ?move))
(<= (legal ?player ?move)
    (legal2 ?player ?move))

; Pawn diagonal move/capture
(<= (legal2 white (move ?x1 ?y1 ?x2 ?y2))
	(true (control white))
	(true (cell ?x1 ?y1 wp))
	(adjFile ?x1 ?x2)
	(nextRank ?y1 ?y2)
	(not (occupies ?x2 ?y2 white)))
(<= (legal2 black (move ?x1 ?y1 ?x2 ?y2))
	(true (control black))
	(true (cell ?x1 ?y1 bp))
	(adjFile ?x1 ?x2)
	(nextRank ?y2 ?y1)
	(not (occupies ?x2 ?y2 black)))
; Straight pawn move
(<= (legal2 white (move ?x ?y1 ?x ?y2))
	(true (control white))
	(true (cell ?x ?y1 wp))
	(file ?x)
	(nextRank ?y1 ?y2)
	(not (occupies ?x ?y2 white))
	(not (occupies ?x ?y2 black)))
(<= (legal2 black (move ?x ?y1 ?x ?y2))
	(true (control black))
	(true (cell ?x ?y1 bp))
	(file ?x)
	(nextRank ?y2 ?y1)
	(not (occupies ?x ?y2 white))
	(not (occupies ?x ?y2 black)))
; King move
(<= (legal2 white (move ?x1 ?y1 ?x2 ?y2))
	(true (control white))
	(true (cell ?x1 ?y1 wk))
	(kingMove ?x1 ?y1 ?x2 ?y2)
	; Force the game along... Kings can't move backwards
	(not (nextRank ?y2 ?y1))
	(not (occupies ?x2 ?y2 white)))
(<= (legal2 black (move ?x1 ?y1 ?x2 ?y2))
	(true (control black))
	(true (cell ?x1 ?y1 bk))
	(kingMove ?x1 ?y1 ?x2 ?y2)
	; Force the game along... Kings can't move backwards
	(not (nextRank ?y1 ?y2))
	(not (occupies ?x2 ?y2 black)))
	
(<= (occupies ?x ?y white)
	(or (true (cell ?x ?y wp))
		(true (cell ?x ?y wk))))
(<= (occupies ?x ?y black)
	(or (true (cell ?x ?y bp))
		(true (cell ?x ?y bk))))
(<= (hasAnyPiece ?player)
	(occupies ?x ?y ?player))

; Physics

(<= (next (step ?np1))
	(true (step ?n))
	(succ ?n ?np1))

(<= (next (cell ?x ?y ?piece))
	(true (cell ?x ?y ?piece))
	(not (affected ?x ?y)))
(<= (next (cell ?x2 ?y2 ?piece))
	(true (cell ?x1 ?y1 ?piece))
	(does ?player (move ?x1 ?y1 ?x2 ?y2)))

; Somewhat unnecessary latches
(<= (next whiteKingCaptured)
	(true (cell ?x2 ?y2 wk))
	(does ?player (move ?x1 ?y1 ?x2 ?y2)))
(<= (next blackKingCaptured)
	(true (cell ?x2 ?y2 bk))
	(does ?player (move ?x1 ?y1 ?x2 ?y2)))
(<= (next whiteKingCaptured)
	(true whiteKingCaptured))
(<= (next blackKingCaptured)
	(true blackKingCaptured))

(<= (affected ?x1 ?y1)
	(does ?player (move ?x1 ?y1 ?x2 ?y2)))
(<= (affected ?x2 ?y2)
	(does ?player (move ?x1 ?y1 ?x2 ?y2)))
	
(<= whiteWins
	(not (true whiteKingCaptured))
	(true (cell ?x 8 wk)))
(<= blackWins
	(not (true blackKingCaptured))
	(true (cell ?x 1 bk))) 

(<= terminal
	whiteWins)
(<= terminal
	blackWins)
(<= terminal
	(true (step 61)))
(<= terminal
	(true whiteKingCaptured)
	(true blackKingCaptured))

(<= (goal white 100)
	whiteWins)
(<= (goal white 50)
	(not whiteWins)
	(not blackWins))
(<= (goal white 0)
	blackWins)
(<= (goal black 100)
	blackWins)
(<= (goal black 50)
	(not whiteWins)
	(not blackWins))
(<= (goal black 0)
	whiteWins)
	
(nextRank 1 2)
(nextRank 2 3)
(nextRank 3 4)
(nextRank 4 5)
(nextRank 5 6)
(nextRank 6 7)
(nextRank 7 8)

(<= (rank ?a)
    (nextRank ?a ?b))
(<= (rank ?b)
    (nextRank ?a ?b))
    
(nextFile a b)
(nextFile b c)
(nextFile c d)
(nextFile d e)
(nextFile e f)
(nextFile f g)
(nextFile g h)

(<= (file ?a)
    (nextFile ?a ?b))
(<= (file ?b)
    (nextFile ?a ?b))
	
(<= (adjFile ?x0 ?x1)
	(nextFile ?x0 ?x1))
(<= (adjFile ?x0 ?x1)
	(nextFile ?x1 ?x0))
(<= (adjRank ?y0 ?y1)
	(nextRank ?y0 ?y1))
(<= (adjRank ?y0 ?y1)
	(nextRank ?y1 ?y0))
	
(<= (kingMove ?x ?y1 ?x ?y2)
	(file ?x)
	(adjRank ?y1 ?y2))
(<= (kingMove ?x1 ?y ?x2 ?y)
	(adjFile ?x1 ?x2)
	(rank ?y))
(<= (kingMove ?x1 ?y1 ?x2 ?y2)
	(adjFile ?x1 ?x2)
	(adjRank ?y1 ?y2))
	
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)
(succ 16 17)
(succ 17 18)
(succ 18 19)
(succ 19 20)
(succ 20 21)
(succ 21 22)
(succ 22 23)
(succ 23 24)
(succ 24 25)
(succ 25 26)
(succ 26 27)
(succ 27 28)
(succ 28 29)
(succ 29 30)
(succ 30 31)
(succ 31 32)
(succ 32 33)
(succ 33 34)
(succ 34 35)
(succ 35 36)
(succ 36 37)
(succ 37 38)
(succ 38 39)
(succ 39 40)
(succ 40 41)
(succ 41 42)
(succ 42 43)
(succ 43 44)
(succ 44 45)
(succ 45 46)
(succ 46 47)
(succ 47 48)
(succ 48 49)
(succ 49 50)
(succ 50 51)
(succ 51 52)
(succ 52 53)
(succ 53 54)
(succ 54 55)
(succ 55 56)
(succ 56 57)
(succ 57 58)
(succ 58 59)
(succ 59 60)
(succ 60 61)
	
; Bases and inputs...
(<= (base (cell ?x ?y wp))
	(file ?x)
	(rank ?y)
	;TODO: Put these back in when the associated bug in the OPNF is fixed. 
	;(distinct ?y 1)
	)
(<= (base (cell ?x ?y bp))
	(file ?x)
	(rank ?y)
	;(distinct ?y 8)
	)
(<= (base (cell ?x ?y wk))
	(file ?x)
	(rank ?y))
(<= (base (cell ?x ?y bk))
	(file ?x)
	(rank ?y))
(base (step 1))
(<= (base (step ?np1))
	(succ ?n ?np1))
(<= (base (control ?player))
	(role ?player))
(base whiteKingCaptured)
(base blackKingCaptured)

(input white noop)
(input black noop)
(<= (input white (move ?x1 ?y1 ?x2 ?y2))
	(kingMove ?x1 ?y1 ?x2 ?y2)
	(not (nextRank ?y2 ?y1)))
(<= (input black (move ?x1 ?y1 ?x2 ?y2))
	(kingMove ?x1 ?y1 ?x2 ?y2)
	(not (nextRank ?y1 ?y2)))